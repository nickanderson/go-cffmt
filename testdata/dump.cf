# set right permissions for ubuntu-guest users to
# run tcpdump / wireshark

bundle agent dumpcapabilities
{

 classes:

   "getcapExists"  expression => fileexists("/sbin/getcap");

   "setcapExists"  expression => fileexists("/sbin/setcap");

   "dumpcapExists" expression => fileexists("/usr/bin/dumpcap");

   "tcpdumpExists" expression => fileexists("/usr/sbin/tcpdump");

  getcapExists.dumpcapExists::
   "FileCapOnDumpcap"
		expression => returnszero("/sbin/getcap  /usr/bin/dumpcap  | grep -q '^/usr/bin/dumpcap = cap_net_admin,cap_net_raw+eip$'",  "useshell");

  getcapExists.tcpdumpExists::
   "FileCapOnTcpdump"
		expression => returnszero("/sbin/getcap  /usr/sbin/tcpdump | grep -q '^/usr/sbin/tcpdump = cap_net_admin,cap_net_raw+eip$'", "useshell");


 files:

  dumpcapExists::
   "/usr/bin/dumpcap"
		perms => mog(0555, root, root);

  tcpdumpExists::
   "/usr/sbin/tcpdump"
		perms => mog(0555, root, root);

  HasDumpCapabilities::
   # delete file that restricts guest users in a lot of ways
   "/etc/apparmor.d"
                delete => tidy,
		depth_search => recurse("0"),
                file_select => by_name("lightdm-guest-session");


 commands:

  setcapExists.dumpcapExists.HasDumpCapabilities.!FileCapOnDumpcap::
   "/sbin/setcap cap_net_raw,cap_net_admin=eip /usr/bin/dumpcap";

  setcapExists.tcpdumpExists.HasDumpCapabilities.!FileCapOnTcpdump::
   "/sbin/setcap cap_net_raw,cap_net_admin=eip /usr/sbin/tcpdump";

  setcapExists.dumpcapExists.!HasDumpCapabilities.FileCapOnDumpcap::
   "/sbin/setcap -r /usr/bin/dumpcap";

  setcapExists.tcpdumpExists.!HasDumpCapabilities.FileCapOnTcpdump::
   "/sbin/setcap -r /usr/sbin/tcpdump";


 reports:
  cfengine_3.Verbose::
   "--> CFE running on $(sys.fqhost) has done cf/dumpcapabilities";


}
